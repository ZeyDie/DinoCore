import joptsimple.internal.Strings

import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.StandardCopyOption

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
    }
}

def libsFolder = 'libs'

repositories {
    mavenCentral()

    flatDir {
        dirs libsFolder
    }
}

apply plugin: 'forge'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = targetCompatibility = '1.7'

compileJava {
    sourceCompatibility = targetCompatibility = '1.7'
}

version = "1.6.4-" + new Date().format('dd.MM.yyyy') + "-deobf"
group = 'net.minecraftforge'
archivesBaseName = "DinoCore"

minecraft {
    version = "1.6.4-9.11.1.963"

    srgExtra "PK: org/bukkit/craftbukkit org/bukkit/craftbukkit/v1_6_R3"
    srgExtra "PK: net/minecraft/src ."
}

dependencies {
    compile fileTree(dir: libsFolder, include: '*.jar')

    // https://mvnrepository.com/artifact/org.projectlombok/lombok
    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
    //annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.26'

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compile group: 'org.jetbrains', name: 'annotations', version: '24.0.0'
}

task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives devJar
    archives sourcesJar
}

jar {
    doFirst {
        manifest {
            attributes(
                    'Implementation-Vendor': 'Cauldron fork by DiverseLab, ZeyDie',
                    'Implementation-Title': 'Cauldron fork by DiverseLab, ZeyDie',
                    'Implementation-Version': 'git-Cauldron-MCPC-Plus-' + project.version,
                    'Forge-Version': '9.11.1.965',
                    'Specification-Vendor': 'Bukkit Team, ZeyDie, Sa1ZeR_',
                    'Specification-Title': 'Bukkit',
                    'Specification-Version': '1.6.4-R2.1-SNAPSHOT',
                    'Main-Class': 'cpw.mods.fml.relauncher.ServerLaunchWrapper',
                    'Class-Path': configurations.runtime.collect {
                        def path = it.getAbsolutePath()

                        if (path.contains(libsFolder))
                            path
                                    .replace(new File(libsFolder).getAbsolutePath(), libsFolder)
                                    .replace('\\', '/')
                        else Strings.EMPTY
                    }.join(' ')
            )
        }
    }
}